#compdef launchctl
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Premitive completion def for OS X launchctl command.
#
#  Subcommands are completed, then for each subcommand, service names are
#  completed (despite expected arguments might not be service names), except
#  for load and unload, where launchd.plists are completed.
#
# ------------------------------------------------------------------------------
# Authors
# -------
#
#  * Zhiming Wang <zmwangx@gmail.com>
#
# ------------------------------------------------------------------------------

if (( CURRENT == 2 )); then
    local subcommands
    (( $+commands[launchctl] )) && subcommands=( ${(@f)"$(command launchctl help | /usr/bin/sed '1,/^Subcommands:$/d; s/[[:blank:]]\([a-z-]\{1,\}\)[[:blank:]]\{1,\}/\1:/')"} )
    _describe -V -t commands 'subcommand' subcommands
    return
fi

# Complete to service names listed in `launchctl list'. Apple's services are
# included as possible matches, but are hidden from the completion listing.
__complete_service_names () {
    local services apple_services third_party_services
    (( $+commands[launchctl] )) && services=( $(command launchctl list | /usr/bin/awk 'NR != 1 { print $3 }') )
    apple_services=( ${(M)services:#com.apple.*} )
    third_party_services=( ${services:#com.apple.*} )
    _describe 'service' third_party_services
    compadd -n -a apple_services
}

# Complete to paths of launchd.plists. System agents and daemons in
# /System/Library are not completed. _files is used as a last resort to
# complete to .plist files.
__complete_launchd_plists () {
    local agents daemons
    agents=( ~/Library/LaunchAgents/*.plist /Library/LaunchAgents/*.plist )
    daemons=( /Library/LaunchDaemons/*.plist )
    _describe -t agents -V 'agent' agents
    _describe -t daemons -V 'daemon' daemons
    _files -g '*.plist'
}

case "$words[2]" in
    load|unload) _arguments '*: :__complete_launchd_plists';;
    *) _arguments '*: :__complete_service_names';;
esac
